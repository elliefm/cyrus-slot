#!/usr/bin/env perl

use warnings;
use strict;

use IPC::System::Simple qw(run);

use FindBin;
use lib "$FindBin::RealBin/../lib";

use Slot;

sub usage;
sub cmd_start;

# FIXME whinge about not being root

my $slot_spec = shift @ARGV;
my @slots = ($slot_spec eq '-a'
    ? Slot::slots()
    : Slot::slots_from_string($slot_spec)
);
exit usage if not scalar @slots;

exit usage if scalar @ARGV;

foreach my $slot (@slots) {
    eval {
	cmd_start("start", $slot);
    };

    if ($@) {
	print STDERR "error: start slot $slot: $@\n";
    }
}

exit 0;

#######################################################

sub usage {
    my ($code) = @_;
    $code = $code || 1;

    print STDERR << "JHLBGK";
usage: $0 slots

JHLBGK

    return $code;
}

#######################################################

sub cmd_start {
    my ($command, $slot) = @_;

    die "not initialised\n" if not -d Slot::slot_dir($slot);

    my $pidfile = Slot::slot_pidfile($slot);

    if (-e $pidfile) {
	my $pid = do {
	    local $/ = undef;
	    open my $fh, '<', $pidfile or die "$pidfile: $!";
	    <$fh>;
	};
	chomp $pid;

	die "pid file exists ($pid). already running?\n";
    }

    my $master = Slot::slot_binary($slot, 'master');

    run $master, '-d', '-p', $pidfile;

    print STDERR "slot $slot started\n";
}
